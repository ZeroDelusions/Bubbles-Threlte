varying vec2 vUv;
varying vec3 vNormal;
varying vec3 vPosition;

uniform float time;
uniform float smoothDeformWaveSin;
uniform float smoothDeformWaveCos;
uniform float smoothDeformSpeedSin;
uniform float smoothDeformSpeedCos;
uniform float smoothDeformStrengthSin;
uniform float smoothDeformStrengthCos;
uniform float noiseFrequency;
uniform float noiseAmplitude;
uniform float seed;

// I dont understand a shit about shaders, help me
// And maybe give me job. Pls.




//
// Description : Array and textureless GLSL 2D/3D/4D simplex 
//               noise functions.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : stegu
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
// 




vec3 mod289(vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x) {
     return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v)
  { 
  const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
  const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

// First corner
  vec3 i  = floor(v + dot(v, C.yyy) );
  vec3 x0 =   v - i + dot(i, C.xxx) ;

// Other corners
  vec3 g = step(x0.yzx, x0.xyz);
  vec3 l = 1.0 - g;
  vec3 i1 = min( g.xyz, l.zxy );
  vec3 i2 = max( g.xyz, l.zxy );

  //   x0 = x0 - 0.0 + 0.0 * C.xxx;
  //   x1 = x0 - i1  + 1.0 * C.xxx;
  //   x2 = x0 - i2  + 2.0 * C.xxx;
  //   x3 = x0 - 1.0 + 3.0 * C.xxx;
  vec3 x1 = x0 - i1 + C.xxx;
  vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
  vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

// Permutations
  i = mod289(i); 
  vec4 p = permute( permute( permute( 
             i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
           + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
           + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

// Gradients: 7x7 points over a square, mapped onto an octahedron.
// The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
  float n_ = 0.142857142857; // 1.0/7.0
  vec3  ns = n_ * D.wyz - D.xzx;

  vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

  vec4 x_ = floor(j * ns.z);
  vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

  vec4 x = x_ *ns.x + ns.yyyy;
  vec4 y = y_ *ns.x + ns.yyyy;
  vec4 h = 1.0 - abs(x) - abs(y);

  vec4 b0 = vec4( x.xy, y.xy );
  vec4 b1 = vec4( x.zw, y.zw );

  //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
  //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
  vec4 s0 = floor(b0)*2.0 + 1.0;
  vec4 s1 = floor(b1)*2.0 + 1.0;
  vec4 sh = -step(h, vec4(0.0));

  vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
  vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

  vec3 p0 = vec3(a0.xy,h.x);
  vec3 p1 = vec3(a0.zw,h.y);
  vec3 p2 = vec3(a1.xy,h.z);
  vec3 p3 = vec3(a1.zw,h.w);

//Normalise gradients
  vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
  p0 *= norm.x;
  p1 *= norm.y;
  p2 *= norm.z;
  p3 *= norm.w;

// Mix final noise value
  vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
  m = m * m;
  return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                dot(p2,x2), dot(p3,x3) ) );
}

void main() {
    vUv = uv;
    vNormal = normal;
    vPosition = position;

    // Use the seed to create a unique offset for each bubble
    float uniqueOffset = fract(sin(seed) * 43758.5453);

    // Adjust noise frequency and amplitude for more natural deformation
    float noiseFreq = noiseFrequency + uniqueOffset * 0.3; // Vary frequency
    float noiseAmp = noiseAmplitude + uniqueOffset * 0.05; // Vary amplitude

    // Generate noise based on position, time, and unique offset
    float noise1 = snoise(vec3(position.x * noiseFreq + uniqueOffset, 
                               position.y * noiseFreq + uniqueOffset, 
                               time * 0.3 + uniqueOffset)) * noiseAmp;
    
    float noise2 = snoise(vec3(position.z * noiseFreq + uniqueOffset, 
                               position.x * noiseFreq + uniqueOffset, 
                               time * 0.2 + uniqueOffset)) * (noiseAmp * 0.5);

    // Combine noise layers for more complex deformation
    float totalNoise = noise1 + noise2;

    // Apply smoother, larger deformations using trigonometric functions
    float smoothDeform = sin(position.y * smoothDeformWaveSin + time * smoothDeformSpeedSin + uniqueOffset * 6.28) * smoothDeformStrengthSin + 
                         cos(position.x * smoothDeformWaveCos + time * smoothDeformSpeedCos + uniqueOffset * 6.28) * smoothDeformStrengthCos;

    // Combine all deformations
    vec3 deformation = normal * (totalNoise + smoothDeform);

    // Apply deformation to the vertex position
    vec3 newPosition = position + deformation;

    // Set the final vertex position
    gl_Position = projectionMatrix * modelViewMatrix * vec4(newPosition, 1.0);
}